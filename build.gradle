plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'net.neoforged.moddev' version '1.0.21'
}

def secrets = new Properties()
def secretsFile = file('secrets.properties')
if (secretsFile.exists()) {
	secretsFile.withInputStream {
		stream -> secrets.load(stream)
	}

	fileTree("secrets").matching {
		include "**/*.properties"
	}.each {
		File file ->
			file.withInputStream {
				stream -> secrets.load(stream)
			}
	}
}

idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}

version = mod_version
group = mod_group_id

base {
	archivesName = 'Risus-'+project.minecraft_version
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
compileJava.options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-this-escape" << "-Werror"

neoForge {
	version = project.neo_version
	validateAccessTransformers = true

	runs {
		configureEach {
			gameDirectory = project.file('run')
			systemProperty 'forge.logging.console.level', 'debug'
			logLevel = org.slf4j.event.Level.DEBUG
		}

		client {
			client()
			systemProperty 'forge.enabledGameTestNamespaces', mod_id
			programArguments.addAll '--username', secrets.getProperty("username") ?: 'Dev', secrets.getProperty("uuid") ? '--uuid' : '', secrets.getProperty("uuid") ?: ''
		}

		server {
			server()
			systemProperty 'forge.enabledGameTestNamespaces', mod_id
			programArgument '--nogui'
		}

		data {
			data()
			programArguments.addAll '--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
		}
	}

	mods {
		risus {
			sourceSet this.sourceSets.main
		}
	}
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
	maven {
		// location of the maven that hosts JEI files since January 2023
		name = "Jared's maven"
		url = "https://maven.blamejared.com/"
	}
	maven {
		// location of a maven mirror for JEI files, as a fallback
		name = "ModMaven"
		url = "https://modmaven.dev"
	}
	maven {
		name = "Curse Maven"
		url "https://cursemaven.com"
	}
}

dependencies {
	// compile against the JEI API but do not include it at runtime
	compileOnly("mezz.jei:jei-1.21-common-api:${jei_version}")
	compileOnly("mezz.jei:jei-1.21-neoforge-api:${jei_version}")
	// at runtime, use the full JEI jar for NeoForge
	runtimeOnly("mezz.jei:jei-1.21-neoforge:${jei_version}")

	implementation "curse.maven:jade-324717:${project.jade_version}"
	compileOnly "vazkii.patchouli:Patchouli:1.21-${project.patchouli_version}:api"
	runtimeOnly "vazkii.patchouli:Patchouli:1.21-${project.patchouli_version}"
}

tasks.named('jar', Jar).configure {
	manifest {
		attributes([
			"Specification-Title"     : "risus",
			"Specification-Vendor"    : "bigdious",
			"Specification-Version"   : "1",
			"Implementation-Title"    : project.name,
			"Implementation-Version"  : project.jar.archiveVersion,
			"Implementation-Vendor"   : "bigdious",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

publishing {
	publications {
		register('mavenJava', MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			url "file://${project.projectDir}/repo"
		}
	}
}
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
	var replaceProperties = [
		minecraft_version      : minecraft_version,
		minecraft_version_range: minecraft_version_range,
		neo_version            : neo_version,
		neo_version_range      : neo_version_range,
		loader_version_range   : loader_version_range,
		mod_id                 : mod_id,
		mod_name               : mod_name,
		mod_license            : mod_license,
		mod_version            : mod_version,
		mod_authors            : mod_authors,
		mod_description        : mod_description
	]
	inputs.properties replaceProperties
	expand replaceProperties
	from "src/main/templates"
	into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}


